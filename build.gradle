buildscript {
    repositories {
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
        classpath "org.grails:grails-docs:${project.ext.properties.grailsDocsVersion ?: grailsVersion}"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
        classpath 'io.github.groovylang.groovydoc:groovydoc-gradle-plugin:1.0.1'
    }
}

//plugins {
//    id 'com.gradle.build-scan' version '2.0.2'
//}

repositories {
    maven { url "https://repo.grails.org/grails/core" }
}

version project.projectVersion

ext {
    commonBuild = 'https://raw.githubusercontent.com/grails/grails-common-build/9531dd5e6d774b86e2d8220aff3bcec3add43320'
}

subprojects {

    version project.projectVersion

    ext {
        userOrg = "grails"
        isGrailsPlugin = name.startsWith('grails-plugin')
        isBuildSnapshot = version.toString().endsWith("-SNAPSHOT")
    }

    repositories {
        maven { url "https://repo.grails.org/grails/core" }
    }

    if(project.name.startsWith("examples-")) {
        if(project.name.startsWith("examples-grails-")) {
            apply plugin:"org.grails.grails-web"
        }

        tasks.withType(Test) {
            testLogging {
                events "failed"
                exceptionFormat "full"
            }
        }
        return
    }

    if(isGrailsPlugin) {
        group "org.grails.plugins"
    }
    else {
        group "org.grails"
    }

    if(project.name.endsWith('docs')) {
        apply from:"${commonBuild}/common-docs.gradle"
        return
    }

    if (isGrailsPlugin) {
        apply plugin:"org.grails.grails-plugin"
    }
    else {
        apply from:"${commonBuild}/common-project.gradle"
    }

    apply from:"${commonBuild}/common-publishing.gradle"

    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/jamesdh/gorm-graphql")
                credentials {
                    username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                    password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
                }
            }
        }
        publications {
            gpr(MavenPublication) {
                if(isGrailsPlugin) {
                    artifactId( project.name - 'grails-plugin-' )
                }
                else if(project.name.contains('/')) {
                    artifactId( project.name.substring(project.name.indexOf('/') + 1) )
                }

                from components.java

                afterEvaluate {
                    artifact source: sourcesJar, classifier: "sources"
                    artifact source: javadocJar, classifier: "javadoc"
                }
                if(isGrailsPlugin) {
                    artifact source:"${project.buildDir}/classes/groovy/main/META-INF/grails-plugin.xml",
                            classifier:"plugin",
                            extension:'xml'
                }
                pom.withXml {
                    def xml = asNode()

                    xml.children().last() + pomInfo
                    // dependency management shouldn't be included
                    def n = xml.get("dependencyManagement")
                    if (n)
                        xml.remove(n)
                }
            }
        }
    }

    dependencies {
        testCompile "org.codehaus.groovy:groovy-test:$groovyVersion"
    }

    tasks.withType(GroovyCompile) {
        configure(groovyOptions) {
            forkOptions.jvmArgs = ['-Xmx1024m']
        }
    }

    tasks.withType(Test) {
        testLogging {
            events "failed"
            exceptionFormat "full"
            showStandardStreams true
        }
    }
}
